import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

repositories {
    maven {
        url "https://mint.splunk.com/gradle/"
    }
    flatDir {
        dirs 'aars'
    }
}

SimpleDateFormat timeStampFormat = new SimpleDateFormat("EEE MMMM dd HH:mm:ss zzz yyyy", Locale.ENGLISH);
String timeStamp = timeStampFormat.format(Calendar.getInstance().getTime());

String playPublicKey = "";
File propsFile = file("/my_properties.properties");
if (propsFile.exists()) {
    Properties props = new Properties();
    props.load(new FileInputStream(propsFile))
    playPublicKey = props.getProperty('GOOGLE_PLAY_PUBLIC_KEY')
    println "- - - - - - - - - - - -"

}

// Google Play key is very important and better to check it twice
// then to get a billion of Force Close reports.

println "GOOGLE_PLAY_PUBLIC_KEY : " + playPublicKey
println "- - - - - - - - - - - -"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.kozaxinan.fixoposcreen"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 20
        versionName "0.9"
    }
    buildTypes {
        debug {
            versionNameSuffix '-debug'
            buildConfigField "boolean", "MY_DEBUG", "true"
            buildConfigField "String", "MY_TIME_STAMP", "\"" + timeStamp + "\""
            // In-App-Billings service compares the signs of apks, so
            // the service won't accept debug build.
            buildConfigField "String", "MY_GOOGLE_PLAY_PUBLIC_KEY", "null"
            debuggable true
//            buildConfigField "String", "MY_GOOGLE_PLAY_PUBLIC_KEY", "\"" + playPublicKey + "\""
//            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "MY_DEBUG", "false"
            buildConfigField "String", "MY_TIME_STAMP", "\"" + timeStamp + "\""
            buildConfigField "String", "MY_GOOGLE_PLAY_PUBLIC_KEY", "\"" + playPublicKey + "\""
            debuggable false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    allprojects {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
//    compile(name: 'Feedback-release', ext: 'aar')
    //    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.splunk.mint:mint:4.0.3'
//    compile 'commons-lang:commons-lang:2.3'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.google.firebase:firebase-ads:10.0.1'
    compile 'com.google.firebase:firebase-core:10.0.1'
}

apply plugin: 'com.google.gms.google-services'