import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

repositories {
    maven {
        url "https://mint.splunk.com/gradle/"
    }
    flatDir {
        dirs 'aars'
    }
}

SimpleDateFormat timeStampFormat = new SimpleDateFormat("EEE MMMM dd HH:mm:ss zzz yyyy", Locale.ENGLISH);
String timeStamp = timeStampFormat.format(Calendar.getInstance().getTime());

String playPublicKey = "";
File propsFile = file("/billing.properties");
if (propsFile.exists()) {
    Properties props = new Properties();
    props.load(new FileInputStream(propsFile))
    playPublicKey = props.getProperty('GOOGLE_PLAY_PUBLIC_KEY')
    println "- - - - - - - - - - - -"
}

// Google Play key is very important and better to check it twice
// then to get a billion of Force Close reports.

println "GOOGLE_PLAY_PUBLIC_KEY : " + playPublicKey
println "- - - - - - - - - - - -"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId "com.kozaxinan.fixoposcreen"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 12
        versionName "0.5"
    }
    buildTypes {
        debug {
            versionNameSuffix '-debug'
            buildConfigField "boolean", "MY_DEBUG", "true"
            buildConfigField "String", "MY_TIME_STAMP", "\"" + timeStamp + "\""
            // In-App-Billings service compares the signs of apks, so
            // the service won't accept debug build.
            buildConfigField "String", "MY_GOOGLE_PLAY_PUBLIC_KEY", "null"
            debuggable false
        }
        release {
            minifyEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "MY_DEBUG", "false"
            buildConfigField "String", "MY_TIME_STAMP", "\"" + timeStamp + "\""
            buildConfigField "String", "MY_GOOGLE_PLAY_PUBLIC_KEY", "\"" + playPublicKey + "\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(name: 'Feedback-release', ext: 'aar')
    compile 'com.splunk.mint:mint:4.0.3'
    compile 'commons-lang:commons-lang:2.3'
    compile 'com.android.support:support-v4:21.0.3'
//    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.google.android.gms:play-services-plus:6.5.87'
}
